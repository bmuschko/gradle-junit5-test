apply plugin: 'groovy'
apply plugin: 'idea'

targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    ext.junitVersion = '1.0.0-M3'
    compile gradleApi()
    compile "org.junit.platform:junit-platform-engine:$junitVersion"
    compile "org.junit.platform:junit-platform-launcher:$junitVersion"
    testCompile gradleTestKit()
    testCompile('org.spockframework:spock-core:1.0-groovy-2.4') {
        exclude module: 'groovy-all'
    }
}

task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

// Add the classpath file to the test runtime classpath
dependencies {
    testRuntime files(createClasspathManifest)
}
